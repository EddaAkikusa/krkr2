/**
 * 環境画像の基底クラス
 */
class KAGEnvImage {

	function dm() {
		global.dm(...) if (kag.debugLevel >= tkdlSimple);
	}

	var layerClass = global.EnvGraphicLayer;
	
	// 処理対象レイヤ
	var targetLayer;
	// トランジション処理中レイヤ(validとは限らない)
	var transLayer;
	// トランジションを待たない
	var transNowait;

	// アクション処理中レイヤ
	var actionLayer;
	var actionTime;

	/// 表示絶対レベル
	var _absolute;

	property absolute {
		getter() {
			return _absolute;
		}
		setter(v) {
			_absolute = v;
			updateFlag = true;
		}
	}

	// 表示処理用原点計算
	var originMode = 9;
	var viewOriginMode = 9;

	// カメラ制御用
	var noshiftMode;
	var nocameraMode;

	// カメラ処理用level指定
	var levelz;
	
	/**
	 * レイヤのコピー処理
	 */
	function onCopyLayer(toback, backlay) {
		if (backlay) {
			stopTransition(true);
			if (toback) {
				if (targetLayer !== void) {
					//dm("裏画面にコピー:" + name);
					//dm("元レイヤ:" + targetLayer + ":" + (targetLayer.parent === kag.back.base ? "裏" : "表"));
					if (targetLayer.parent === kag.fore.base && targetLayer.visible) {
						var absolute = targetLayer.absolute;
						var layer = createLayer(kag.back);
						layer.assign(targetLayer);
						transLayer = targetLayer;
						layer.absolute = absolute;
						targetLayer = layer;
						//dm("複製レイヤ:" + targetLayer + ":" + (targetLayer.parent === kag.back.base ? "裏" : "表"));
					} else {
						//dm("廃棄");
						transLayer = targetLayer;
						targetLayer = void;
					}
				}
			} else {
				if (targetLayer !== void) {
					//dm("表画面にコピー:" + name);
					if (targetLayer.parent === kag.back.base && targetLayer.visible) {
						//dm("表に移動");
						var absolute = targetLayer.absolute;
						targetLayer.parent = kag.fore.base;
						targetLayer.absolute = absolute;
					} else {
						//dm("廃棄");
						transLayer = targetLayer;
						targetLayer = void;
					}
				}
			}
		}
	}
	
	// デフォルト情報
	var init;
	var name;
	
    // スキップ状態か
    function isSkip() {
        return env.kag.skipMode != SKIP_FORCE && (env.kag.skipMode || (env.kag.noeffect && env.kag.noeffectEnabled));
    }

    // 各種情報を保持する環境
    var env;

	/// 表示状態変更
    // BOTH      バストアップ＋フェイス (標準）
    // BU        バストアップ
    // FACE      フェイス
	// SHOW      表示状態（自動選択）
	// CLEAR     消去状態 (標準)
    // INVISIBLE 非表示
	var _disp;
	property disp {
		getter() {
			return _disp;
		}
		setter(v) {
			if (v == SHOW) {
				if (isClear()) {
					if (init !== void && init.noPose !== void) {
						v = init.noPose ? FACE : BOTH;
					} else {
						v = BOTH;
					}
				} else {
					v = _disp;
				}
			}
			if (v !== _disp) {
				_disp = v;
				doShow = true;
				redraw = true;
				reposition = true;
				if (type !== void) {
					doType = true;
				}
			}
		}
	}

	// バストアップ表示中
    function isShowBU() {
        return _disp <= BU;
    }

	// フェイス表示中
    function isShowFace() {
        return (_disp == BOTH && env.bothFace)|| _disp == FACE;
    }

	// 表示されているか
	function isShow() {
        return _disp <= FACE;
	}

	// 消去状態か
	function isClear() {
		return _disp == CLEAR;
	}

	// 更新フラグ
	var updateFlag;
    // 再描画指示フラグ
	var _redraw;
	property redraw {
		getter() {
			return _redraw;
		}
		setter(v) {
			_redraw = v;
			//dm(name + ":redraw:" + Scripts.getTraceString());
		}
	}
	
    // 初期化処理
    var resetFlag;

    /**
     * 時間用最初期化処理
     */
	function setRedraw() {
		if (isShowBU()) {
			redraw = true;
		}
    }

	/**
	 * 配置再調整用
	 */
	function recalcPosition() {
		if (targetLayer !== void) {
			targetLayer.recalcPosition();
		}
    }
	
	// -----------------------------------------------------------------------
	// レイヤ制御
	// -----------------------------------------------------------------------

	// 表示状態
	var doShow;
	
    // 種別
    var _type;
    var doType;
    property type {
        getter() {
            return _type;
        }
        setter(v) {
            _type = v;
            doType = (v !== void);
			updateFlag = true;
        }
    }

    // 透明度
    var opacityFrom;
    var opacityTime;
    var opacityAccel;
	var opacityNowait;
    var _opacity;
    var doOpacity;
    property opacity {
        getter() {
            return _opacity;
        }
        setter(v) {
            _opacity = v;
            doOpacity = (v !== void);
			updateFlag = true;
        }
    }

    // 回転
    var rotateFrom;
    var rotateTime;
    var rotateAccel;
	var rotateNowait;
    var _rotate;
    var doRotate;
    property rotate {
        getter() {
            return _rotate;
        }
        setter(v) {
            _rotate = v;
            doRotate = (v !== void);
			updateFlag = true;
        }
    }

    // ズーム処理
    var zoomFrom;
    var zoomTime;
    var zoomAccel;
	var zoomNowait;
    var _zoom;
	var doZoom;
    property zoom {
        getter() {
            return _zoom;
        }
        setter(v) {
			_zoom = v;
			doZoom = (v !== void);
			updateFlag = true;
        }
    }

    // 回転原点指定
    var _afx;
    var _afy;
    var doAffine;
    property afx {
        getter() {
            return _afx;
        }
        setter(v) {
            _afx = v;
            doAffine = (v !== void);
			updateFlag = true;
        }
    }
    property afy {
        getter() {
            return _afy;
        }
        setter(v) {
            _afy = v;
            doAffine = (v !== void);
			updateFlag = true;
        }
    }

	// ラスター処理
	var rasterFrom;
	var rasterTime;
	var rasterAccel;
	var rasterNowait;
	var rasterLines;
	var rasterCycle;
    var _raster;
	var doRaster;
    property raster {
        getter() {
			return _raster;
		}
        setter(v) {
			_raster = v;
			doRaster = (v !== void);
			updateFlag = true;
        }
    }
	
    // 表示位置座標
    var xpos;
    var ypos;
    var xposFrom;
    var yposFrom;
	var moveTime;
	var moveAccel;
	var moveNowait;

    // 位置変更
    var reposition;
    
    // アクション処理
	var doStopAction;
	var actionList;
	var currentActionList;

    // 画像補整指定
	var _grayscale;
    property grayscale {
        getter() {
            if (env.colorall) {
                return env.grayscale;
            } else {
                return _grayscale;
            }
        }
        setter(v) {
            _grayscale = v;
            if (!env.colorall) {
				redraw = true;
            }
        }
    }

    var _rgamma;
    property rgamma {
        getter() {
            if (env.colorall) {
                return env.rgamma;
            } else {
                return _rgamma;
            }
        }
        setter(v) {
            _rgamma = v;
            if (!env.colorall) {
                redraw = true;
            }
        }
    }

    var _ggamma;
    property ggamma {
        getter() {
            if (env.colorall) {
                return env.ggamma;
            } else {
                return _ggamma;
            }
        }
        setter(v) {
            _ggamma = v;
            if (!env.colorall) {
                redraw = true;
            }
        }
    }
    
    var _bgamma;
    property bgamma {
        getter() {
            if (env.colorall) {
                return env.bgamma;
            } else {
                return _bgamma;
            }
        }
        setter(v) {
            _bgamma = v;
            if (!env.colorall) {
                redraw = true;
            }
        }
    }

    // 画像補整指定
	var _blurx;
	property blurx {
		getter() {
			if (env.colorall) {
				return env.blurx;
            } else {
				return _blurx;
            }
        }
		setter(v) {
			_blurx = v;
			if (!env.colorall) {
				redraw = true;
            }
        }
    }

	var _blury;
	property blury {
		getter() {
			if (env.colorall) {
				return env.blury;
            } else {
				return _blury;
            }
        }
		setter(v) {
			_blury = v;
			if (!env.colorall) {
				redraw = true;
            }
        }
    }
	
	// 色状態の初期化
    function resetColor(param, elm) {
		_grayscale = void;
		_rgamma = void;
        _ggamma = void;
		_bgamma = void;
		_blurx  = void;
		_blury  = void;
        if (!env.colorall) {
            redraw = true;
        }
    }
    
    // フェード指定のデフォルト値
    property fadeValue {
        getter() {
            return env.fadeValue;
        }
    }
    
    /**
     * コンストラクタ
     * @param env 環境
     */
	function KAGEnvImage(env, name) {
		this.env = env;
		this.name = name;
		actionList = new Array();
		currentActionList = new Array();
		_disp = CLEAR;
    }

	function initImage() {
        type = void;
        opacityFrom = void;
        opacityTime = void;
        opacityAccel = void;
		opacityNowait = void;
        opacity = void;
        rotateFrom = void;
        rotateTime = void;
        rotateAccel = void;
		rotateNowait = void;
        rotate = void;
        zoomFrom = void;
        zoomTime = void;
        zoomAccel = void;
		zoomNowait = void;
        zoom = void;
        afx = void;
        afy = void;
        rasterFrom = void;
        rasterTime = void;
        rasterAccel = void;
		rasterNowait = void;
		rasterLines = void;
		rasterCycle = void;
		raster = void;
		xpos     = void;
        ypos     = void;
        xposFrom = void;
        yposFrom = void;
        moveTime = void;
		moveAccel = void;
		moveNowait = void;
        reposition = false;

        _rgamma = void;
		_ggamma = void;
		_bgamma = void;
		_grayscale = void;
		_blurx = void;
		_blury = void;
        
        resetFlag = false;
        actionList.clear();
		currentActionList.clear();
        _disp = CLEAR;
    }

	/**
	 * 表示諸元を初期化する
	 * @param elm パラメータ指定
	 */
	function initFile(elm) {
		if (elm.type === void) type = void;
		if (elm.opacity === void) opacity = void;
		if (elm.rotate === void) rotate = void;
		if (elm.zoom === void) zoom = void;
		if (elm.raster === void) raster = void;
		if (elm.afx === void) afx = void;
		if (elm.afy === void) afy = void;
		clearAction(true);
	}
	
    function finalize() {
		stopTransition(true);
		if (targetLayer !== void) {
			invalidate targetLayer;
		}
		clearAction(true);
		invalidate actionList;
		invalidate currentActionList;
    }

    function setXPos(cmd, elm) {
		if (moveTime === void) {
			moveTime  = elm.time;
			moveAccel = elm.accel;
			moveNowait = elm.nowait;
		}
		xposFrom = calcRelative(xpos, getFrom(cmd), env.xmax);
		xpos     = calcRelative(xpos, getTo(cmd), env.xmax);
		//dm("X位置指定:", xpos, xposFrom, moveTime);
        reposition = true;
    } 

    function setYPos(cmd, elm) {
		if (moveTime === void) {
			moveTime  = elm.time;
			moveAccel = elm.accel;
			moveNowait = elm.nowait;
        }
		yposFrom = calcRelative(ypos, getFrom(cmd), env.ymax);
		ypos     = calcRelative(ypos, getTo(cmd), env.ymax);
		//dm("Y位置指定:", ypos, yposFrom, moveTime);
        reposition = true;
    }

    /**
     * アクション情報の追加
     */
	function addAction(action) {
		// 規定のモジュールは排除する
		//dm("アクション情報登録");
		if (action instanceof "Dictionary") {
			if (action.module == "LayerFadeToModeModule") {
				opacity     = action.opacity;
				opacityTime = action.time;
				opacityAccel = action.accel;
				opacityNowait = action.nowait;
			} else if (action.module == "LayerToRotateModule") {
				rotate = action.angle;
				rotateTime = action.time;
				rotateAccel = action.accel;
				rotateNowait = action.nowait;
			} else if (action.module == "LayerNormalZoomModule") {
				zoom = action.zoom;
				zoomTime = action.time;
				zoomAccel = action.accel;
				zoomNowait = action.nowait;
			} else if (action.module == "LayerAccelMoveModule") {
				xpos = action.x;
				ypos = action.y;
				moveTime = action.time;
				moveAccel = 1;
				moveNowait = action.nowait;
				reposition = true;
			} else if (action.module == "LayerNormalMoveModule") {
				xpos = action.x;
				ypos = action.y;
				moveTime = action.time;
				moveAccel = 0;
				moveNowait = action.nowait;
				reposition = true;
			} else if (action.module == "LayerDecelMoveModule") {
				xpos = action.x;
				ypos = action.y;
				moveTime = action.time;
				moveAccel = -1;
				moveNowait = action.nowait;
				reposition = true;
			} else {
				actionList.add(action);
				updateFlag = true;
			}
		} else if (action instanceof "Array") {
			actionList.add(action);
			updateFlag = true;
		}
	}

	// nowait つきのアクションかどうか
	function isNowaitAction(action) {
		if (typeof action == "Object")  {
			if (action instanceof "Dictionary") {
				if (action.nowait !== void) {
					return action.nowait;
				}
			} else if (action instanceof "Array") {
				for (var i=0;i<action.count;i++) {
					var ac = action[i];
					if (typeof ac == "Object" && ac instanceof "Dictionary") {
						if (ac.nowait !== void) {
							return ac.nowait;
						}
					}
				}
			}
		}
		return false;
	}
	
    /**
	 * アクション情報の消去
	 * @param all 全アクション指定
     */
	function clearAction(all) {
		//dm("アクション解除指示:" + name + ":" + all);
		for (var i=currentActionList.count-1;i>=0;i--) {
			var info = currentActionList[i];
			if (all || !isNowaitAction(info)) {
				currentActionList.erase(i);
			}
		}
    }

	/**
	 * アクション情報のコピー
	 * @param act  元アクション情報
	 * @param time 時間上書き指定
	 * @param elm  タグによる上書き用パラメータ情報
	 * @return システム登録用アクション情報
	 */
	function copyActionInfo(act, time, over) {
		var action = %[];
		if (act !== void) {
			var names = [];
			names.assign(act);
			if (act.module !== void || act.moduleName !== void) {
				// 旧スタイル
				for (var i=0; i<names.count; i+= 2) {
					var name = names[i];
					var elm  = names[i+1];
					action[name] = elm;
				}
				if (time !== void) {
					action.time = time;
				}
				// パラメータのコピー
				if (over !== void) {
					foreach(over, function(name, value, over, action) {
						if (actionParam[name] !== void) {
							action[name] = value;
						}
					}, action);
				}
			} else {
				// 新スタイル
				for (var i=0; i<names.count; i+= 2) {
					var name = names[i];
					var elm  = names[i+1];
					if (elm instanceof "Dictionary") {
						// 辞書の場合はクローンして時間指定
						var e = %[];
						(Dictionary.assign incontextof e)(elm);
						if (time !== void) {
							e.time = time;
						}
						// パラメータのコピー
						if (over !== void) {
							e.time   = +over.time if over.time !== void;
							e.delay  = +over.delay if over.delay !== void;
							e.nowait = +over.nowait if over.nowait !== void;
						}
						action[name] = e;
					} else {
						// そうでない場合はそのまま
						action[name] = elm;
					}
				}
			}
		}
		return action;
	}
	
    /**
     * アクションを設定
     * @param name アクション名
     * @param elm パラメータ
     */
	function setAction(name, elm) {
		if (env.actions !== void) {
			var info;
			if ((info = env.actions[name]) !== void) {
				if (info instanceof "Dictionary") {
					var action = copyActionInfo(info, info.time, elm);
					if (action.module !== void && action.module == "") {
						//dm("アクション停止処理:" + name);
						stopAction(true);
					} else {
						addAction(action);
					}
					return true;
				} else if (info instanceof "Array") {
					//dm("配列アクション:" + name);
					var arrayAction = [];
					for (var i=0;i<info.count;i++) {
						var i = info[i];
						if (typeof i == "String") {
							// 文字列の場合再帰的に参照
							i = env.actions[i];
						}
						if (i !== void && i instanceof "Dictionary") {
							var action = copyActionInfo(i, i.time, elm);
							arrayAction.add(action);
						}
					}
					addAction(arrayAction);
					return true;
                }
                return false;
            }
			// 個別記述
			if (elm !== void && name.substring(0,5) == "Layer") {
				var moduleType;
				if (typeof global[name] != "undefined") {
					moduleType = global[name];
					if (typeof moduleType == "Object") {
						// アクションモジュール名の場合
						var action = %[];
						foreach(elm, function(name, value, elm, action) {
							if (actionParam[name] !== void) {
								action[name] = value;
							}
						}, action);
						action.module = name;
						action.time   = +elm.time   if elm.time !== void;
						action.delay  = +elm.delay  if elm.time !== void;
						action.nowait = +elm.nowait if elm.nowait !== void;
						addAction(action);
						return true;
					} else {
						kag.errorCmd("不明なレイヤアクション指定:" + name);
					}
				} else {
					kag.errorCmd("不明なレイヤアクション指定:" + name);
				}
			}
        }
        return false;
    }

    /**
     * アクションを解除
     */
	function stopAction() {
		clearAction(true);
		doStopAction = true;
		updateFlag = true;
    }

	// --------------------------------------------------------------------
	// トランジション処理
	// --------------------------------------------------------------------
	
    // 画面更新設定
    var trans;
	var transall; // 全トランジションが必要かどうかのフラグ
	
    /**
     * トランジション情報の取得
     */
    function getTrans(name, elm) {
		// 名前指定で上書き
		var tr = %[];
		if (name == "void" || name == "none") {
			return tr;
		}
		var info;
		if (env.transitions !== void && (info = env.transitions[name]) !== void) {
			//dm("登録ずみのトランジション:" + name);
			// コピー
			(Dictionary.assign incontextof tr)(info, false); 
			// パラメータのコピー
			foreach(elm, function(name, value, elm, tr) {
				if (transitionParam[name] !== void) {
					//dm("パラメータコピー:" + name + ":" + value);
					tr[name] = value;
				}
			}, tr);
			return tr;
		} else if (elm != null && (transitionName[name] !== void)) {
			//dm("規定のシステムトランジション:" + name);
			var tr = %[];
			// 規定のトランジション
			// パラメータのコピー
			foreach(elm, function(name, value, elm, tr) {
				if (transitionParam[name] !== void) {
					tr[name] = value;
				}
			}, tr);
			tr.method = name;
			return tr;
		}
    }

    /**
	 * トランジションを設定
	 * @param name トランジション名
	 * @param elm パラメータ
	 * @return トランジションが設定された
	 */
	function setTrans(name, elm) {
		//dm("トランジション設定:" + name);
		var tr = getTrans(name, elm);
		if (tr !== void) {
			trans = tr;
			return true;
		}
		return false;
    }

	// 自動トランジション
	var autoTrans;

	// 自動トランジションを登録
	function setAutoTrans(list) {
		for (var i=0;i<list.count;i++) {
			var param = list[i];
			if (param !== void) {
				if (typeof param == "String") {
					var tr = getTrans(param);
					if (tr !== void && tr.method != void) {
						autoTrans = tr;
						//dm("自動トランジション指定:" + tr.method + ":" + tr.time);
						return;
					}
				} else if (param instanceof "Dictionary") {
					autoTrans = %[];
					(Dictionary.assign incontextof autoTrans)(param); 
					return;
				}
			}
		}
	}

	/**
	 * 表示処理用の自動トランジションの指定
	 */
	function setDispAutoTrans() {
	}
	
	// --------------------------------------------------------------
	
	/**
	 * 状態更新処理
     */
	function updateLayer(layer) {
		
		if (resetFlag) {
			layer.visible = isShowBU();
			layer.reset();
            _type    = void;
			_opacity = void;
			_rotate = void;
			_zoom = void;
			_raster = void;
			_afx = void;
            _afy = void;
			clearAction(true);
			_rgamma = void;
			_ggamma = void;
			_bgamma = void;
            _grayscale = void;
			_blurx = void;
			_blury = void;
			resetFlag = false;
		}

		if (doShow) {
			layer.visible = isShowBU();
			doShow = false;
		}
		if (doAffine) {
			layer.afx = afx;
			layer.afy = afy;
			doAffine = false;
		}
		if (doOpacity) {
			//dm("透明度変更:" + opacity + ":" + opacityTime); 
			if (opacityFrom !== void) {
				layer.opacity = opacityFrom;
				opacityFrom = void;
			}
			layer.setOpacityTime(opacity, opacityTime, opacityAccel, opacityNowait);
			doOpacity = false;
		}
		if (doRotate) {
			//dm("回転変更:" + rotate + ":" + rotateTime);
			if (rotateFrom !== void) {
				layer.rotate = rotateFrom;
				rotateFrom = void;
			}
			layer.setRotateTime(rotate, rotateTime, rotateAccel, rotateNowait);
			doRotate = false;
		}
		if (doZoom) {
			//dm("ズーム変更:" + zoom + ":" + zoomTime);
			if (zoomFrom !== void) {
				layer.zoom = zoomFrom;
				zoomFrom = void;
			}
			layer.setZoomTime(zoom, zoomTime, zoomAccel, zoomNowait);
			doZoom = false;
		}
		if (doRaster) {
			//dm("ズーム変更:" + zoom + ":" + zoomTime);
			if (rasterFrom !== void) {
				layer.raster = rasterFrom;
				rasterFrom = void;
			}
			layer.setRasterTime(raster, rasterLines, rasterCycle, rasterTime, rasterAccel, rasterNowait);
			doRaster = false;
		}

		if (doType) {
			//dm("合成種別変更");
			layer.type = type;
			doType = false;
		}
		if (doStopAction) {
			//dm("アクション停止");
			layer.stopAction();
			doStopAction = false;
		}
		// アクション処理
		if (actionList.count > 0) {
			//dm("アクション適用開始:" + actionList.count);
			for (var i=0;i<actionList.count;i++) {
				var action = actionList[i];
				dm("アクション開始:" + typeof action + ":" + action);
				layer.beginAction(action);
				// 実行中アクションの記録（保存用）
				if (action instanceof "Dictionary") {
					if (action.time == 0) {
						//dm("アクションを記録:" + i);
						currentActionList.add(action);
					}
				} else if (action instanceof "Array") {
					if (action.count > 0 && action[action.count - 1].time == 0) {
						//dm("アクションを記録:" + i + " count:" + action.count);
						currentActionList.add(action);
					}
				}
			}
			actionList.clear();
		}
		// アクションの結果の吸収
		var result = layer.getActionResult();
		if (result !== void) {
			//dm("アクション結果取得");
			//dm("不透明度:" + result.opacity);
			//dm("回転量:" + result.rotate);
			//dm("拡大率:" + result.zoom);
			//dm("left:" + result.left);
			//dm("top:" + result.top);
			_opacity = result.opacity if result.opacity !== void;
			_rotate  = result.rotate  if result.rotate  !== void;
			_zoom    = result.zoom    if result.zoom    !== void;
			_raster  = result.raster  if result.raster  !== void;
			xpos = result.left if result.left !== void;
			ypos = result.top  if result.top  !== void;
		}
	}

	/**
	 * 相対値計算 Action.tjs からのコピー
	 * @param value 値指定
	 * @param orig オリジナルの値
	 */
	function getRelative(value, orig) {
		if (typeof value == "String" && (value.indexOf("@") >= 0)) {
			return Scripts.eval(value.replace(/@/, (string)orig));
		} else {
			return +value;
		}
	}
	
	function applyPropMoveAction(action, prop, thisprop) {
		if (thisprop === void) {
			thisprop = prop;
		}
		var info = action[prop];
		if (info !== void && info.handler == "MoveAction") {
			if (info.value !== void) {
				this[thisprop] = getRelative(info.value, this[thisprop]);
			}
		}
	}
	
	function applyMoveAction(action) {
		if (action.module !== void) {
			// 旧スタイルアクションは無視の方向
			return;
		}
		applyPropMoveAction(action, "opacity");
		applyPropMoveAction(action, "rotate");
		applyPropMoveAction(action, "zoom");
		applyPropMoveAction(action, "raster");
		applyPropMoveAction(action, "left", "xpos");
		applyPropMoveAction(action, "top",  "ypos");
	}

	function updateSkip() {
		// アクション廃棄処理
		if (actionList.count > 0) {
			for (var i=0;i<actionList.count;i++) {
				var action = actionList[i];
				if (action instanceof "Dictionary") {
					applyMoveAction(action);
				} else if (action instanceof "Array") {
					for (var j=0;j<action.count;j++) {
						applyMoveAction(action[j]);
					}
				}
			}
			actionList.clear();
		}
	}
	
    var commands = %[
    tagname : null, 
    time : null,
    type : function(param, elm) {
        type =  global[param];
    } incontextof this,
    opacity : function(param, elm) {
		opacityFrom = calcRelative(opacity, getFrom(param), 100);
        opacity     = calcRelative(opacity, getTo(param), 100);
        opacityTime = isSkip() ? 0 : +elm.time;
        opacityAccel = +elm.accel;
    } incontextof this,
    rotate : function(param, elm) {
        rotateFrom = calcRelative(rotate, getFrom(param), 360);
        rotate     = calcRelative(rotate, getTo(param), 360);
        rotateTime = isSkip() ? 0 : +elm.time;
        rotateAccel = +elm.accel;
    } incontextof this,
	zoom : function(param, elm) {
        zoomFrom = calcRelative(zoom, getFrom(param), 100);
        zoom     = calcRelative(zoom, getTo(param), 100);
        zoomTime = isSkip() ? 0 : +elm.time;
        zoomAccel = +elm.accel;
    } incontextof this,
    afx : function(param, elm) {
        afx = param;
    } incontextof this,
    afy : function(param, elm) {
        afy = param;
    } incontextof this,
	raster : function(param, elm) {
		rasterFrom = calcRelative(raster, getFrom(param), 100);
        raster     = calcRelative(raster, getTo(param), 100);
        rasterTime = isSkip() ? 0 : +elm.time;
		rasterAccel = +elm.accel;
		rasterLines = elm.rasterlines;
		rasterCycle = elm.rastercycle;
    } incontextof this,
	rasterlines : null,
	rastercycle : null,
    reset : function(param, elm) {
		resetFlag = true;
		updateFlag = true;
    } incontextof this,
    grayscale : function(param, elm) {
        grayscale = param;
    } incontextof this,
    rgamma : function(param, elm) {
        rgamma = param;
    } incontextof this,
    ggamma : function(param, elm) {
        ggamma = param;
    } incontextof this,
    bgamma : function(param, elm) {
        bgamma = param;
    } incontextof this,
	blurx : function(param, elm) {
        blurx = param;
    } incontextof this,
	blury : function(param, elm) {
        blury = param;
    } incontextof this,
	blur : function(param, elm) {
		blurx = blury = param;
    } incontextof this,
    resetcolor : this.resetColor incontextof this,
    action : setAction incontextof this,
    stopaction : stopAction incontextof this,
	stoptrans : function(param) { stopTransition(true); } incontextof this,
	xpos : this.setXPos incontextof this,
	ypos : this.setYPos incontextof this,
	origin : function(cmd,elm) { originMode = +cmd; reposition = true; } incontextof this,
    vorigin : function(cmd,elm) { viewOriginMode = +cmd; reposition = true; } incontextof this,
	noshift : function(cmd,elm) { noshiftMode = +cmd; } incontextof this,
	nocamera : function(cmd,elm) { nocameraMode = +cmd; } incontextof this,
	trans : this.setTrans incontextof this,
	notrans : function(param) { trans = %[]; } incontextof this,
	show : function(param) { disp = SHOW; },
	hide : function(param) { disp = CLEAR; },
	accel : null, // 無視
	fade : null, // 無視
	sync : null, // 無視
	nosync : null, // 無視
	nowait : null, // 無視
	waitmovie : null,
	waitanime : null,
		];

    /**
     * コマンドの実行
     * @param cmd コマンド
     * @param param パラメータ
     * @param elm 他のコマンドも含む全パラメータ
     * @return 実行が行われた場合 true
     */
	function doCommand(cmd, param, elm) {
        //dm("コマンド処理:" + cmd + " パラメータ:" + param);
        var func;
        if ((func = commands[cmd]) !== void) {
            if (func != null) {
                func(param, elm);
            }
            return true;
        }
        // 後で考えたほうがいいかも？
        if (actionParam[cmd] !== void) {
            return true;
        }
        if (transitionParam[cmd] !== void) {
            return true;
        }
        return false;
    }

    /**
     * セーブ処理
     */
    function onStore(f) {
		f.name    = name;

		f.type    = type;
		f.opacity = opacity;
        f.rotate  = rotate;
        f.zoom    = zoom;
		f.afx     = afx;
		f.afy     = afy;
        f.raster  = raster;
        f.xpos    = xpos;
        f.ypos    = ypos;
        f.disp    = disp;

		f.actionList = [];
		(Array.assign incontextof f.actionList)(currentActionList);
		//for (var i=0;i<currentActionList.count;i++) {
		//dm("アクション保存:" + currentActionList[i].handler);
		//}
		f.grayscale = _grayscale;
        f.rgamma    = _rgamma;
        f.ggamma    = _ggamma;
        f.bgamma    = _bgamma;
		f.blurx     = _blurx;
		f.blury     = _blury;

		f.absolute = absolute;
		f.originMode = originMode;
        f.viewOriginMode = viewOriginMode;
	}

    /**
     * ロード処理
     */
    function onRestore(f) {
		name = f.name;
		
        type = f.type;
        opacity     = f.opacity;
        opacityFrom = void;
        opacityTime = void;
        opacityAccel = void;
        rotate      = f.rotate;
        rotateFrom  = void;
        rotateTime  = void;
        rotateAccel = void;
        zoom        = f.zoom;
        zoomFrom    = void;
        zoomTime    = void;
        zoomAccel   = void;
		raster      = f.raster;
        rasterFrom    = void;
        rasterTime    = void;
		rasterAccel   = void;
		rasterLines   = void;
		rasterCycle   = void;
		afx         = f.afx;
		afy         = f.afy;
		if (f.actionList !== void) {
			for (var i=0;i<f.actionList.count;i++) {
				addAction(f.actionList[i]);
				//dm("アクション復帰:" + f.actionList[i].handler);
            }
        }
		disp      = f.disp;
		xpos      = f.xpos;
		xposFrom  = void;
        ypos      = f.ypos;
        yposFrom  = void;
		moveTime  = void;
        moveAccel = void;
		moveNowait = void;

        if (isShowBU()) {
            reposition = true;
        }

		_grayscale = f.grayscale;
        _rgamma    = f.rgamma;
        _ggamma    = f.ggamma;
        _bgamma    = f.bgamma;
		_blurx     = f.blurx;
		_blury     = f.blury;
		if (_grayscale != void ||
            _rgamma    != void ||
			_ggamma    != void ||
			_bgamma    != void ||
			_blurx     != void ||
			_blury     != void) {
            redraw = true;
        }

		absolute = f.absolute;
		originMode = f.originMode;
        viewOriginMode = f.viewOriginMode;
	}

	// レイヤのサイズ調整用
	function setSize(layer) {
	}
	
	// このメソッドを実装する
	// function drawLayer(layer)

    /**
     * レイヤ配置処理(標準)：左上原点
     * @param layer 処理対象レイヤ
     */
	function calcPosition(layer) {
		//dm(name + ":位置指定:" + xpos + "," + ypos + " time:" + moveTime);
		var l = (int)xpos;
		var t = (int)ypos;
		if (moveTime !== void && moveTime > 0) {
			if (xposFrom !== void || yposFrom !== void) {
				var fl = xposFrom !== void ? (int)xposFrom : l;
				var ft = yposFrom !== void ? (int)yposFrom : t;
				layer.setPos(fl, ft);
			}
			layer.setMove(l, t, moveTime, moveAccel, moveNowait);
		} else {
			layer.setMove(l, t);
		}
		xposFrom = void;
		yposFrom = void;
		moveTime = void;
	}

	/**
	 * トランジションの強制中断
	 */
	function stopTransition(all) {
		if (transLayer !== void) {
			if (all || !transNowait) {
				//dm(name + "トランジション中断");
				if (isvalid transLayer) {
					transLayer.stopTransition();
				}
				if (isvalid transLayer) {
					invalidate transLayer;
				}
				transLayer = void;
			}
		}
	}

	/*
	 * トランジション実行エントリ
	 * @param trans トランジション情報
	 * KAG にトランジション実行命令を差し込む
	 */
	function beginTransition(trans) {
		if (trans === void || trans.method === void || trans.time == 0 || isSkip()) {
			kag.addTag("forelay");
		} else {
			// 実際のトランジション実行
			trans.children = true;
			if (trans.method === void) {
				trans.method = "crossfade";
			}
			kag.addTag("trans", trans);
			if (trans.transwait !== void) {
				// 時間待ち
				kag.addTag("wait", %[ time : (int)trans.time + (int)trans.transwait, trans:true ]);
			} else {
				// トランジション待ち
				kag.addTag("wt");
			}
			// メッセージ窓状態同期
			kag.addTag("syncmsg");
		}
    }

	/*
	 * トランジション実行エントリ
	 * @param trans トランジション情報
	 * KAG にトランジション実行命令を差し込む
	 */
	function beginTransShow(trans) {
		//dm("トランジション表示実行");
		
		stopTransition(true);
		
		// 新レイヤの準備（裏につくる）
		_update(kag.back);

		// 旧レイヤを準備
		transLayer          = createLayer(kag.fore);
		transLayer.visible  = true;
		transLayer.absolute = targetLayer.absolute;
		transLayer.name = "old_" + transLayer.name;

		// トランジション実行
		trans.deldest = true;
		trans.children = true;
		trans.target = transLayer;
		trans.src    = targetLayer;
		kag.addTag("laytrans", trans);
	}
	
	/*
	 * トランジション実行エントリ
	 * @param trans トランジション情報
	 * KAG にトランジション実行命令を差し込む
	 */
	function beginTransHide(trans) {
		//dm("トランジション消去実行");

		stopTransition(true);
		
		// 旧レイヤの破棄準備
		transLayer      = targetLayer;
		transLayer.name = "old_" + transLayer.name;
		targetLayer = void;

		// 新レイヤの準備（裏につくる：生成されないはず）
		_update(kag.back);

		var layer = createLayer(kag.back);
		layer.visible  = true;
		layer.absolute = transLayer.absolute;
		trans.delsrc = true;

		// トランジション実行
		trans.deldest = true;
		trans.children = true;
		trans.target = transLayer;
		trans.src    = layer;
		kag.addTag("laytrans", trans);
	}

	/*
	 * トランジション実行エントリ
	 * @param trans トランジション情報
	 * KAG にトランジション実行命令を差し込む
	 */
	function beginTransUpdate(trans) {
		//dm("トランジション更新実行");

		stopTransition(true);
		
		// 旧レイヤの破棄準備
		if (targetLayer !== void && targetLayer.visible) {
			transLayer = targetLayer;
			targetLayer = createLayer(kag.back);
			targetLayer.assign(transLayer);
		} else {
			transLayer  = targetLayer;
			targetLayer = void;
		}
		transLayer.name = "old_" + transLayer.name;

		// 新レイヤの準備（裏につくる）
		_update(kag.back);
		
		//dm("旧レイヤ表示状態:" + transLayer  + ":" + transLayer.visible);	
		//dm("新レイヤ表示状態:" + targetLayer + ":" + targetLayer.visible);
		
		// トランジション実行
		trans.children = true;
		trans.deldest = true;
		trans.target = transLayer;
		trans.src    = targetLayer;
		kag.addTag("laytrans", trans);
	}

	function beginTransActionShow(trans) {
		//dm("アクション表示実行");
		doShow = false;
		_update(kag.fore);
		var action = copyActionInfo(trans.showaction, trans.time);
		action["visvalue"] = %[ handler:MoveAction, value:100, time:trans.time ];
		action.target = targetLayer;
		kag.addTag("action", action);
		actionLayer = targetLayer;
	}
	
	function beginTransActionHide(trans) {
		//dm("アクション消去実行");
		// アクション実行
		doShow = false;
		_update(kag.fore);
		var layer = targetLayer;
		var action = copyActionInfo(trans.hideaction, trans.time);
		action["visvalue"] = %[ handler:MoveAction, value:0, time:trans.time ];
		action.target = targetLayer;
		action.del = true; // 終了時にレイヤを破棄
		kag.addTag("action", action);
		actionLayer = targetLayer;
		targetLayer = void;
	}
	
	function beginTransActionUpdate(trans) {
		//dm("アクション更新実行");

		var hidetime = 0;
		var showtime = 0;
		
		// 古いレイヤの処理
		var oldlayer = targetLayer;
		targetLayer = void;

		if (oldlayer !== void) {
			targetLayer = createLayer(kag.fore);
			targetLayer.assign(oldlayer);
			targetLayer.visible = false;

			oldlayer.name = "old_" + oldlayer.name;
			var time = trans.hidetime !== void ? trans.hidetime : trans.time;
			var delay = trans.hidedelay;
			var action = copyActionInfo(trans.hideaction, time, %[delay:delay]);
			action["visvalue"] = %[ handler:MoveAction, value:0, time:time, delay:delay];
			action.target = oldlayer;
			action.del = true;
			kag.addTag("action", action);
			hidetime = delay + time;
		}

		// 新しいレイヤの処理
		doShow = false;
		_update(kag.fore);
		if (targetLayer !== void) {
			var time = trans.showtime !== void ? trans.showtime : trans.time;
			var delay = trans.showdelay;
			var action = copyActionInfo(trans.showaction, time, %[delay:delay]);
			action["visvalue"] = %[ handler:MoveAction, value:100, time:time, delay:delay];
			action.target = targetLayer;
			kag.addTag("action", action);
			showtime = delay + time;
		}

		//dm("旧レイヤ表示状態:" + oldlayer  + ":" + oldlayer.visible);	
		//dm("新レイヤ表示状態:" + targetLayer + ":" + targetLayer.visible);
		
		actionLayer = showtime > hidetime ? targetLayer : oldlayer;
		actionTime  = showtime > hidetime ? showtime : hidetime;
	}
	
	/**
	 * メッセージ窓消去
	 */
	function hideMessage(trans) {
		if (trans !== void && trans.msgoff) {
			if (isSkip()) kag.addTag("msgoff");
			else       kag.insertTag("msgoff");
		}
    }

	/**
	 * 単純更新
	 */
	function _update(base) {
		if ((isShowBU() || targetLayer !== void) && (updateFlag || redraw || reposition)) {
			//dm("更新処理実行:" + name + " updateFlag:" + updateFlag + " redraw:" + redraw + " reposition:" + reposition);
			if (targetLayer === void || targetLayer.parent !== base.base) {
				var layer = createLayer(base);
				if (targetLayer !== void) {
					layer.assign(targetLayer);
					invalidate targetLayer;
				}
				targetLayer = layer;
			}
			// 変更されてることがあるので常に反映
			targetLayer.absolute = +absolute if (absolute !== void);

			if (redraw) {
				targetLayer.reset();
				drawLayer(targetLayer);
				if (grayscale) {
					targetLayer.doGrayScale();
				}
				if (rgamma != void || ggamma != void || bgamma != void) {
					targetLayer.adjustGamma(rgamma == void ? 1.0 : rgamma, 0, 255,
									  ggamma == void ? 1.0 : ggamma, 0, 255,
									  bgamma == void ? 1.0 : bgamma, 0, 255);
				}
				if (blurx || blury) {
					targetLayer.doBoxBlur(blurx, blury);
				}
				// 描画処理中に必要な情報更新が行われる場合があるので
				// オフセット調整はこの位置で行う
				targetLayer.updateOffsetInfo();
			}
			if (redraw || reposition) {
				setSize(targetLayer);
				calcPosition(targetLayer);
			}
			redraw = false;
			reposition = false;
			updateLayer(targetLayer);
		} else {
			updateSkip();
		}
		updateFlag = false;
	}

	// タグ処理の帰り値
	var _ret;
	property ret {
		getter() {
			return _ret;
		}
		setter(v) {
			if (v === void) {
				_ret = 0;
            } else {
                if (v < ret) {
                    _ret = v;
                }
            }
        }
    }

	/**
	 * KAG 個別コマンド処理
	 * @param elm 引数
	 */
    function command(elm) {
		var names = [];
		names.assign(elm);
		for (var i=0; i<names.count; i+= 2) {
			if (!doCommand(names[i], names[i+1], elm)) {
				kag.errorCmd(name + ":未知のコマンド:" + names[i]);
			}
		}
	}
	
	/**
	 * KAG タグ処理
	 * @param elm 要素
	 */
	function tagfunc(elm) {
		
		kag.updateBeforeCh = 1;
		ret = void;
		trans = void;
		transall = false;
		autoTrans = void;
		
		// コマンド実行前の表示状態
		var prevAbsolute = absolute;
		var prevShow = isShowBU();

		command(elm);

		// 0:非表示 1:表示 2:消去 3:更新
		var isAbsoluteChange = prevAbsolute != absolute;
		var mode = (prevShow ? 2 : 0) + (isShowBU() ? 1 : 0);
		//dm("表示モード:" + mode);

		if (trans === void) {
			if (elm.fade) {
				var fadeTime = elm.fade;
				trans = %[
					"time" => fadeTime > 1 ? fadeTime : fadeValue,
					"method" => "crossfade",
					];
			} else {
				// 表示・消去指定に対応
				if (mode == 1 || mode == 2) {
					setDispAutoTrans();
				}
				trans = autoTrans;
			}
		}

		// キャラクタ強制消去
		if (trans !== void && trans.charoff) {
			foreach(env.characters, function(name,value,dict) {
				value.disp = CLEAR;
			} incontextof this);
		}

		var waitMode = 0; // 0:待ちなし 1:アクションまち 2:トランジションまち
		
		if (!kag.skipNoDisp) {
			//dm("更新処理開始" + name);
			if (env.transMode) {
				//dm("トランジション処理中");
				env.update(env.transMode == 1 ? kag.back : kag.fore);
			} else if (isSkip()) {
				// スキップ中または描画なし
				env.update(kag.fore);
			} else {
				if (trans === void || trans.time == 0) {
					//dm("瞬間更新");
					env.update(kag.fore);
				} else if (trans.method !== void) {
					//dm("画像変更（トランジション）");
					waitMode = 2;
					if (mode == 1) {
						beginTransShow(trans);
						transNowait = elm.nowait;
					} else if (mode == 2) {
						beginTransHide(trans);
						transNowait = elm.nowait;
					} else {
						if (transall || trans.transall || isAbsoluteChange) {
							//dm("色合い変更またはabsolute変更あり");
							// absolute が変化する場合は強制で全体トランジション
							if (elm.nosync || trans.nosync) {
								kag.errorCmd("absolute変化・色変化・transall指定の場合は nosync できません:" + name);
							}
							kag.stopAllTransitions();
							kag.backupLayer(EMPTY, true);
							env.update(kag.back);
							beginTransition(trans);
							waitMode = 0;
						} else {
							//dm("absolute変更なし");
							beginTransUpdate(trans);
							transNowait = elm.nowait;
						}
					}
				} else {
					waitMode = 1;
					if (mode == 1) {
						beginTransActionShow(trans);
					} else if (mode == 2) {
						beginTransActionHide(trans);
					} else {
						beginTransActionUpdate(trans);
					}
				}
			}
			hideMessage(trans);

			var syncMode;
			if (!env.envinfo.nosynctrans) {
				syncMode = !elm.nosync && !(trans !== void && trans.nosync);
			} else {
				syncMode = elm.sync || (trans !== void && trans.sync);
			}
			
			// シンクロ指定
			if (waitMode == 1) {
				// アクショントランス待ち
				if (syncMode) {
					if (trans.transwait !== void) {
						// 時間待ち
						kag.addTag("wait", %[ time:actionTime + (int)trans.transwait, action:true ]);
					} else {
						// アクション待ち
						kag.addTag("wact", %[ target:actionLayer ]);
					}
				}
			} else if (waitMode == 2) {
				// 通常トランジション待ち
				if (syncMode) {
					if (trans.transwait !== void) {
						// 時間待ち
						kag.addTag("wait", %[ time:(int)trans.time + (int)trans.transwait, trans:true ]);
					} else {
						// トランジション待ち
						kag.addTag("wt", %[]);
						//kag.addTag("wt", %[ target:transLayer ]);
					}
				}
			} else {
				if (targetLayer !== void && targetLayer.visible) {
					if (elm.sync) {
						ret = kag.waitAction(targetLayer);
					}
					if (elm.waitmovie) {
						ret = kag.waitLayerMovie(targetLayer);
					}
					if (elm.waitanime) {
						ret = kag.waitLayerAnimation(targetLayer);
					}
				}
			}

		} else {
			updateSkip();
		}
		//dm("タグ処理完了 targetLayer:" + targetLayer);

		return ret;
	}

	function createLayer(base) {
		return new layerClass(base.base.window, base.base, this);
	}
}
