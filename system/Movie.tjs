// Movie.tjs - ムービー再生
// Copyright (C)2001-2005, W.Dee and contributors  改変・配布は自由です

class Movie extends VideoOverlay
{
	var owner;
	var lastStatus = "unload"; // 直前のステータス
	var opened = false;
	var id = 0; // ムービーオブジェクトID

	function Movie(owner,id=0)
	{
		super.VideoOverlay(...);

		this.owner = owner;
		this.id = id;
	}

	function finalize()
	{
		if(lastStatus == "play") stop();
		super.finalize(...);
	}

	function onStatusChanged(status)
	{
		// ステータスの変更があった
		if(lastStatus == "play" && status == "stop")
		{
			owner.onMovieStop(id); // 停止
			super.close();
		}

		lastStatus = status;
	}

	function onPeriod(type)
	{
		// period イベントが発生した
		owner.onMoviePeriod(id,type);
	}

	function onCallbackCommand(cmd, arg)
	{
		// コールバックコマンド
		if(cmd == "Go")
		{
			var spos = arg.indexOf('/');
			if(spos == -1)
			{
				// ストレージ指定がない
				owner.process('', '*' + arg);
			}
			else
			{
				// ストレージ指定がある
				var label = arg.substring(spos + 1);
				if(label != '') label = '*' + label;
				owner.process(arg.substring(0, spos), label);
			}
		}
		else if(cmd == "Eval")
		{
			Scripts.eval(arg);
		}
	}

	property canWaitStop
	{
		getter
		{
			// 待てるかどうか
			return lastStatus == "play";
		}
	}

	function open(storage)
	{
		// open オーバーライド
		try
		{
			super.open(storage);
			opened = true;
		}
		catch(e)
		{
			if(e.message.indexOf(".dll") != -1) throw e;
			dm("ムービー " + storage + " を再生できません : " +
				e.message);
			return;
		}
	}

	function play(storage)
	{
		// play オーバーライド
		try
		{
			if(!opened) super.open(storage);
			super.play();
			opened = false;
		}
		catch(e)
		{
			if(e.message.indexOf(".dll") != -1) throw e;
			dm("ムービー " + storage + " を再生できません : " +
				e.message);
			return;
		}
	}

	function stop()
	{
		// stop オーバーライド
		super.stop(...);
		super.close();
	}

	function setOptions(elm)
	{
		// elm からオプションを設定
		visible = +elm.visible if elm.visible !== void;
		var l = left, t = top, w = width, h = height;
		var set = false;
		(set = true, l = +elm.left) if elm.left !== void;
		(set = true, t = +elm.top) if elm.top !== void;
		(set = true, w = +elm.width) if elm.width !== void;
		(set = true, h = +elm.height) if elm.height !== void;
		if(set) setBounds(l, t, w, h);
		
		//
		position = +elm.position if elm.position !== void;
		loop = +elm.loop if elm.loop !== void;
		frame = +elm.frame if elm.frame !== void;
		if( elm.mode !== void )
		{
			if( elm.mode == "layer" )
				mode = vomLayer;
			else
				mode = vomOverlay;
		}
	}

	function setVideoLayer(layer,elm)
	{
		if( elm.channel !== void )
		{
			if( elm.channel == 1 )
				layer1 = layer;
			else
				layer2 = layer;
		}
	}
	function setSegment(elm)
	{
		var startFrame = 0;
		startFrame = +elm.start if elm.start !== void;
		if( elm.end !== void )
			super.setSegmentLoop( startFrame, +elm.end );
	}
	function setPeriod(elm)
	{
		if( elm.frame !== void )
			super.setPeriodEvent( +elm.frame );
	}
}


