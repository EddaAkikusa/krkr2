/**
 * KAG 前景レイヤのレベル制御用
 */
class EnvLevelLayer {

    /// 表示レベル
    var level;
    /// 表示絶対レベル
	var absolute;
	/// 復帰フラグ
	var restore;
	
	// 描画対象レイヤ
	var targetLayer;
	
    /// 位置変更
    var front;
    var back;

    /**
     * コンストラクタ
     */
    function EnvLevelLayer() {
		level = void;
    }

    function finalize() {
		player.trashLayer(targetLayer);
    }

	/**
	 * レイヤのコピー処理
	 */
	function onCopyLayer(toback, backlay) {
		if (backlay) {
			if (toback) {
				if (targetLayer !== void) {
					//dm("裏画面にコピー:" + name);
					//dm("元レイヤ:" + targetLayer + ":" + (player.isForeLayer(targetLayer) ? "表" : "裏"));
					if (player.isForeLayer(targetLayer) && targetLayer.visible) {
						targetLayer = player.getNewLayer(this, false, targetLayer);
						//dm("複製レイヤ:" + targetLayer + ":" + (player.isForeLayer(targetLayer) ? "表" : "裏"));
					} else {
						//dm("廃棄");
						targetLayer = player.trashLayer(targetLayer);
					}
				}
			} else {
				if (targetLayer !== void) {
					//dm("表画面にコピー:" + name);
					if (!player.isForeLayer(targetLayer) && targetLayer.visible) {
						//dm("表に移動");
						player.toForeLayer(targetLayer);
					} else {
						//dm("廃棄");
						targetLayer = player.trashLayer(targetLayer);
					}
				}
			}
		}
	}
	
    /**
     * レイヤ情報の取得
     * @param base fore / back      
     * 同時にレベルに応じた front/back の処理も実行する
     */
	function getLayer(isfore) {
		if (targetLayer === void || player.isForeLayer(targetLayer) != isfore) {
			targetLayer = player.getNewLayer(this, isfore, targetLayer);
		}
		if (restore) {
			targetLayer.level    = +level    if (level !== void);
			targetLayer.absolute = +absolute if (absolute !== void);
			restore = false;
		}
		if (level === void) {
			level = env.defaultLevel;
		}
		player.toLevel(targetLayer, level);
		if (front) {
			player.toFront(targetLayer);
		} else if (back) {
			player.toBack(targetLayer);
        }
		front = back = void;
		absolute = targetLayer.absolute;
		return targetLayer;
    }

    /**
     * レベル内最前列
     */
    function setFront() {
        front = true;
		updateFlag = true;
    }

    /**
     * レベル内最後列
     */
    function setBack() {
        back = true;
		updateFlag = true;
    }

    /**
     * レベルの指定
     */
    function setLevel(cmd, elm) {
		level = cmd;
		absolute = void;
    } 

}
