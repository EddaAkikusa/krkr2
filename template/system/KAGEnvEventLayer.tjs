/**
 * イベント画像を処理するレイヤ
 */
class KAGEnvEventLayer extends KAGEnvLayer {

    /**
	 * コンストラクタ
     * @param env 環境
	 * @param name 環境のレイヤ名
	 */
	function KAGEnvEventLayer(env) {
		super.KAGEnvLayer(env, "event");
    }

	/**
	 * イベント用にファイル名のかけかえ
	 */
    function setImageFile(file, elm) {
		var eventTrans;
		var eventInfo;
		if (env.events !== void && (eventInfo = env.events[file]) !== void) {
			eventTrans = eventInfo.trans;
			file = eventInfo.image;
			if (elm.xpos !== void && eventInfo.xoff !== void) {
				elm.xpos = eventInfo.xoff;
			}
			if (elm.ypos !== void && eventInfo.xoff !== void) {
				elm.ypos = eventInfo.yoff;
			}
		}
		super.setImageFile(file, elm);
		setAutoTrans([eventTrans, env.envinfo.eventTrans, env.envinfo.envTrans]);
        return true;
    }

	// ------------------------------------------------------

	function getLayer(base) {
		if (base === void) {
			base = env.transMode == 1 ? kag.back : kag.fore;
		}
		return base[name];
	}

	// ------------------------------------------------------
	
    /**
	 * コマンドの実行
	 * @param cmd コマンド
	 * @param param パラメータ
	 * @param elm 他のコマンドも含む全パラメータ
     * @return 実行が行われた場合 true
     */
    function doCommand(cmd, param, elm) {

		// 共通コマンド
		if (super.doCommand(cmd, param, elm)) {
			return true;
        }
		
		// 画像のロード
		return setImageFile(cmd, elm);
	}
	// ------------------------------------------------------

	/**
	 * 表示処理用の自動トランジションの指定
	 */
	function setDispAutoTrans() {
		setAutoTrans([env.envinfo.eventTrans, env.envinfo.envTrans]);
	}
}
